apply plugin: 'osgi'

description = 'Islandora 8 Fcrepo Indexer'

dependencies {
    implementation group: 'org.apache.camel', name: 'camel-core', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-blueprint', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-http4', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-jackson', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-jsonpath', version: versions.camel
    implementation(project(':islandora-event-support')) {
        transitive = true
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j
    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo 
    testImplementation group: 'org.apache.camel', name: 'camel-test-blueprint', version: versions.camel
    testImplementation group: 'org.apache.servicemix.bundles', name: 'org.apache.servicemix.bundles.xerces', version: versions.xercesServiceMix
    testImplementation group: 'org.ow2.asm', name: 'asm-commons', version: versions.asmCommons
    testImplementation group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version: versions.junitToolbox
    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: versions.slf4j
}

test {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
}

jar {
    manifest {
      description project.description
      docURL project.docURL
      vendor project.vendor
      license project.license

      instruction 'Import-Package', 'org.apache.camel.component.http4,' +
                            "org.apache.camel.jsonpath," +
                            "org.apache.camel;version=\"${versions.camel}\"," +
                            "!ca.islandora.alpaca.support.event," +
                            defaultOsgiImports
      instruction 'Bundle-ClassPath', ".,islandora-event-support-${project.version}.jar"
      instruction 'Export-Package', 'ca.islandora.alpaca.indexing.fcrepo'
    }
    from {
        configurations.runtimeClasspath.findAll{ !it.directory && it.name.startsWith( 'islandora-event-support' ) }
    }
}

artifacts {
    archives (file('build/cfg/main/ca.islandora.alpaca.indexing.fcrepo.cfg')) {
        classifier 'configuration'
        type 'cfg'
    }
}

test {
    testLogging {
        // Make sure output from standard out or error is shown
        // in Gradle output.
        // Uncomment the below 3 lines while debugging.
        events 'standard_out', 'standard_error'
        exceptionFormat = 'full'
        displayGranularity = 0
    }
}
